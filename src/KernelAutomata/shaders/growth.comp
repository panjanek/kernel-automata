#version 450

layout (local_size_x = 16, local_size_y = 16) in;

layout (rgba32f, binding = 0) readonly  uniform image2D fieldIn;
layout (rgba32f, binding = 1) readonly  uniform image2D convIn;
layout (rgba32f, binding = 2) writeonly uniform image2D fieldOut;

uniform float uMu;
uniform float uSigma;
uniform float uDt;
uniform int   uSize;

float gaussianGrowth(float u)
{
    float x = (u - uMu) / uSigma;
    return 2.0 * exp(-x * x) - 1.0;
}

void main()
{
    ivec2 p = ivec2(gl_GlobalInvocationID.xy);
    if (p.x >= uSize || p.y >= uSize)
        return;

    float a = imageLoad(fieldIn, p).r;
    float u = imageLoad(convIn, p).r;

    // Normalize inverse FFT
    u /= float(uSize * uSize);

    float g = gaussianGrowth(u);
    float next = clamp(a + uDt * g, 0.0, 1.0);

    imageStore(fieldOut, p, vec4(next, 0.0, 0.0, 0.0));
}
