#version 450

layout (local_size_x = 16, local_size_y = 16) in;

layout (rgba32f, binding = 0) readonly  uniform image2D fieldIn;
layout (rgba32f, binding = 1) readonly  uniform image2D myConvIn;
layout (rgba32f, binding = 2) readonly  uniform image2D competeConvIn;
layout (rgba32f, binding = 3) writeonly uniform image2D fieldOut;

uniform float uMu;
uniform float uSigma;
uniform float uDt;
uniform int   uSize;
uniform float uMyWeight;
uniform float uCompeteWeight;
uniform float uDecay;

float gaussianGrowth(float u)
{
    float x = (u - uMu) / uSigma;
    return 2.0 * exp(-x * x) - 1.0;
}

void main()
{
    ivec2 p = ivec2(gl_GlobalInvocationID.xy);
    if (p.x >= uSize || p.y >= uSize)
        return;

    float current = imageLoad(fieldIn, p).r;
    float my = imageLoad(myConvIn, p).r;
    float compete = imageLoad(competeConvIn, p).r;

    // Normalize inverse FFT
    my /= float(uSize * uSize);
    compete /= float(uSize * uSize);

    float growth =  gaussianGrowth(my * uMyWeight + compete * uCompeteWeight);

    float res = current + uDt * (growth - current * uDecay);

    float next = clamp(res, 0.0, 1.0);
    imageStore(fieldOut, p, vec4(next, 0.0, 0.0, 0.0));
}
